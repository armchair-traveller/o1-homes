// WIP: The stuff in here isn't complete. This is just PoC code for extension and plans. 
// Some of it is fleshed out, some of it isn't done yet. I'm just putting it here so I can discuss it.
// the goal is an autocomplete search for cities, filter by a city, and then intersect with filtered price range / bedroom results

// To get syntax highlighting, install Fauna extension for VSCode
// for reference, range filtering index documents
Paginate(Range(Match(Index("Home_by_price")), [0], [100000]))

CreateIndex({
  name: "Home_sorted_by_price",
  source: Collection("Home"),
  values: [
    { field: ["data", "price"] },
    { field: ["ref"] }
  ]
})

// TODO: Example GraphQL UDF resolver for searching price range
Update(
  Function("homesByPrice"),
  {
    body: Query(
      Lambda(
      ["minPrice", "maxPrice"],
        Map(
          Select(
            ["data"],
            Paginate(
              Range(
                Match(Index("Home_sorted_by_price")),
                Var("minPrice"),
                Var("maxPrice")
              )
            )
          ),
          Lambda(["price", "ref"], Get(Var("ref")))
        )
      )
    )
  }
)

// This finds home refs by the city "San Mateo" and when paginated returns as an arr of refs
// Address by city index
// Property by address ref index
// Home by property ref index
// none of these indexes are included in the auto index so we need to create them
// Note: This is confirmed working
Paginate(
  Join(
    Join(
      Match(Index("Address_by_city"), "San Mateo"), // second param should be a Var for city
      Index("Property_by_Address_ref")
    ),
    Index("Home_by_Property_ref")
  )
)

// This is for autocomplete search. Use Distinct with this index to get unique city names.
// - Make sure that you use the first autocomplete result for looking up the city name, for good UX (get homes while typing)
// Searching index for Address cities with ngrams (full text search, exact contains)
// This might be a bit inefficient given no unique constraint for values (NOT terms) only. It's not as simple as turning
// on unique constraint, as that would prevent you from having multiple addresses with the same city.
CreateIndex({
  name: 'search_city',
  // we actually want to sort to get the shortest word that matches first
  source: [
    {
      // If your collections have the same property that you want to access you can pass a list to the collection
      collection: [Collection('Address')],
      fields: {
        searchTerms: Query(
          Lambda(
            'Address',
            Distinct(
              Union(
                Let(
                  {
                    // TODO: Reduce this array if you want less ngrams per word.
                    indexes: [0, 1, 2, 3, 4, 5, 6, 7, 8, 9],
                    indexesFiltered: Filter(
                      Var('indexes'),
                      // filter out the ones below 0
                      Lambda('l', GT(Var('l'), 0))
                    ),
                    ngramsArray: Map(
                      Var('indexesFiltered'),
                      // you can also cleanup the input data by surrounding the Lowercase in a replace
                      Lambda('l', NGram(LowerCase(Select(['data', 'city'], Var('Address'))), Var('l'), Var('l')))
                    ),
                  },
                  Var('ngramsArray')
                )
              )
            )
          )
        ),
      },
    },
  ],
  terms: [{ binding: 'searchTerms' }],
  values: [{ field: ['data', 'city'] }],
})

// Example query that gives an arr of refs
Map(
  Select(
    ["data"],
    Paginate(
      Range(
        Match(Index("Home_sorted_by_price")),
        0, // edit w/ price later
        1000000 // should return only one result, "San Mateo" city
      )
    )
  ),
  Lambda(["price", "ref"], Var("ref"))
)

// Note: The below are plans to finish features, they are not complete yet but the implementation is pretty much as
//       straightforward as described. 
// TODO: Home by bedrooms index
// TODO: Range function over index for bedrooms GTE 6
// TODO: Intersecting the results of the above queries (city, min/max price, bedrooms) to get the final result as a resolver
// TODO: Furthermore, implement pagination params for the above queries. Although the above are a challenge enough due to
//       blank "" values being a possibility to account for. If a serverless function is used, it's possible to write
//       logic in much less time and in a much more readable manner. However, it does introduce a small latency penalty.

// Consider: We will need a refs index for each index where we indexing values other than the refs. This is because they
//           are not able to be intersected in a performant manner otherwise (the alternative is an unperformant Filter).